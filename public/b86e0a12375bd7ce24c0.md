---
title: '書庫ファイル(.tar)の展開と作成(1): 値のメモリへの保存、メモリからの取り出し'
tags:
  - Haskell
  - tar
  - ポインタ
  - メモリ
private: true
updated_at: '2018-12-13T11:42:27+09:00'
id: b86e0a12375bd7ce24c0
organization_url_name: null
slide: false
ignorePublish: false
---
書庫ファイル(.tar)の展開と作成
===========================

はじめに
-------

Haskellによるメモリいじりは楽しいな。

目次
----

現在のところ1のみ。2からあとは、comming soon。また、目次は書き進めるうちに変更するかもしれない。

1. 値のメモリへの保存、メモリからの取り出し
2. ヘッダの読み込み
3. ヘッダの書き出し
4. 書庫ファイルの展開
5. 書庫ファイルの作成
6. ユニットテスト
7. 単純なファイル以外のファイルへの対応

対象読者
-------

バイト単位でファイルをいじりたくなったHaskeller。メモリをいじりたくなったHaskeller。状態モナド(State, StateT)については理解している。

範囲
----

単純なファイルとディレクトリからなるディレクトリ構造を.tarファイルにまとめる。また、そのようなファイルを展開する。gnu tarでデフォルトのやりかたで作成した書庫ファイルについて、「展開 -> 作成」としたときに、もとのファイルと等しくなるようにする。

メモリの確保
-----------

メモリの確保と解放には、関数mallocとfreeが使える。

```haskell
> :module Foreign.Ptr
> :module + Foreign.Marshal
> p <- malloc :: IO (Ptr Int)
> p
0x00007fb2b4000ab0
> free p
```

メモリのfreeを忘れるというのは、太古より受けつがれてきた人類の過ちである。freeを忘れないように関数mallocは、できるだけ使わないようにしよう。かわりに、関数allocaを使う。

ここでは、対話環境での書きやすさのためにScopedTypeVariables拡張を使っておこう。

```haskell
> :set -XScopedTypeVariables
> alloca $ \(p :: Ptr Int) -> return p
0x000000420058dc20
```

このように関数allocaを使って

```haskell
alloca $ \p -> [pを使った処理]
```

のように書くことで、freeし忘れることをふせぐことができる。

メモリに対する読み書き
-------------------

さて、これでメモリを確保して、それを解放するという、わくぐみが用意できた。メモリから値を読み出したり、メモリに値を書き込んだりしてみよう。
ここで、型クラスStorableというものが出てくる。Preludeに用意された多くの型がStorableクラスのインスタンスになっている。

```hs
> :module Foreign.Storable
> :info Storable
...
instance Storable Word
instance Storable Int
instance Storable Float
instance Storable Double
instance Storable Char
instance Storable Bool
instance Storable ()
```

1バイト単位でメモリの内容を見ていくにはWord8型の値を使うと便利だ。モジュールData.Wordも読み込もう。読み込むモジュールの数が増えてきたので、import文だけのファイルを作って、それを読み込むことにしよう。

```hs:usePtr.hs
import Data.Word
import Foreign.Ptr
import Foreign.Marshal
import Foreign.Storable
```

このファイルを読み込んでおき、まずは32ビット符号なし整数を書き込み、それから8ビット符号なし整数として取り出してみる。

```hs
> :load usePtr.hs
> alloca $ \p -> poke p (0x01020304 :: Word32) >> mapM (peek . (p `plusPtr`)) [0 .. 3] :: IO [Word8]
[4,3,2,1]
```

自作の型のメモリに対する読み書き
----------------------------

自分で作った型も、Storableクラスのインスタンスにすれば、メモリに対して読み書きが可能になる。

```hs:usePtr.hs
data Suit = Spade | Hart | Diamond | Club deriving Show

instance Storable Suit where
        sizeOf _ = 1
        alignment _ = 1
        peek p = toEnum . fromIntegral <$> peek (castPtr p :: Ptr Word8)
        poke p = poke (castPtr p :: Ptr Word8) . fromIntegral . fromEnum
```

関数sizeOfは、この型の値が何バイトぶんの大きさかを示し、alignmentはメモリのアドレスが何の倍数であるかを示す。alignmentは、1、2、4、8あたりを指定することになるだろう。これについてはハードウェアについての知識が必要になる。
対話環境に読み込んで試してみる。

```hs
> :load usePtr.hs
> alloca $ \p -> poke p Diamond >> peek (castPtr p :: Ptr Word8)
2
> alloca $ \p -> poke (castPtr p :: Ptr Word8) 3 >> peek (p :: Ptr Suit)
Club
```

メモリを確保して、それにDiamondを書き込み、Word8として取り出すと2がかえる。また、Word8として3を書き込んで、Suitとして取り出すとClubがかえる。

自作の型の値のファイルに対する読み込み/書き出し
--------------------------------------

自作の型の値をファイルから読み込んだり、ファイルに書き出したりしてみよう。ここでは、より一般的にStorableクラスのインスタンスである型の値について、読み込みと書き出しとを定義してみる。

### 読み込み

まず、言語拡張ScopedTypeVariablesを設定する。

```haskell:usePtr.hs
{-# LANGUAGE ScopedTypeVariables #-}
```

拡張を使わなくても関数asTypeOfを使ってもいいが、この拡張を使ったほうがコードがすこしシンプルになる。

関数hReadStorableを定義してみよう。

```haskell:usePtr.hs
hReadStorable :: forall a . Storable => Handle -> IO (Maybe a)
hReadStorable h = alloca $ \p -> do
        let     t = undefined :: a
        n <- hGetBuf h p $ sizeOf t
        if n == sizeOf t
                then Just <$> peek p
                else return Nothing
```

値tはa型のエラー値である。a型におけるsizeOf関数の結果を入手するために必要だ。全体の流れとしては関数hGetBufによって、メモリにファイルの内容を読み込み、そこから関数peekによってa型の値を取り出している。nは読み込んだバイト数であり、これがa型の値のサイズと異なるときには、読み込みに失敗する。例外としてもいいが、ここではMaybe値のNothingで失敗を表現している。

### 書き出し

書き出しについては、よりシンプルに書ける。

```haskell:usePtr.hs
hWriteStorable :: Storable a => Handle -> a -> IO ()
hWriteStorable h x = alloca $ \p -> do
        poke p x
        hPutBuf h p $ sizeOf x
```

関数pokeで値をメモリに書き込む。そして、メモリの内容をファイルに書き込む。

### 読み出しと書き込みを試す

```haskell
> :load usePtr.hs
> h <- openFile "tmp.suit" WriteMode
> hWriteStorable h Club
> hClose h
> h <- openFile "tmp.suit" ReadMode
> hReadStorable h :: IO (Maybe Suit)
Just Club
```

おわりに
-------

「書庫ファイル(.tar)の展開と作成(1)」は、書庫ファイルのヘッダの読み込みと書き出しの、準備体操となっている。ここで見たhReadStorableやhWriteStorableなどは、標準ライブラリにあっても良さそうなものだが、hoogleでの検索では引っかからなかった。
